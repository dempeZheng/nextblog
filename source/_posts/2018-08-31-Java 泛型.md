---
layout: post
title:  "Java泛型"
date:  2018-08-31 22:39:04
type: java
categories: [java]
keywords: jvm,泛型,桥方法,type erasure
---
Java泛型（generics）是JDK 5中引入的一个新特性，允许在定义类和接口的时候使用类型参数（type parameter）。声明的类型参数在使用时用具体的类型来替换。泛型最主要的应用是在JDK 5中的新集合类框架中。对于泛型概念的引入，开发社区的观点是褒贬不一。从好的方面来说，泛型的引入可以解决之前的集合类框架在使用过程中通常会出现的运行时刻类型错误，因为编译器可以在编译时刻就发现很多明显的错误。而从不好的地方来说，为了保证与旧有版本的兼容性，Java泛型的实现上存在着一些不够优雅的地方。当然这也是任何有历史的编程语言所需要承担的历史包袱。后续的版本更新会为早期的设计缺陷所累。  

开发人员在使用泛型的时候，很容易根据自己的直觉而犯一些错误。比如一个方法如果接收List<Object>作为形式参数，那么如果尝试将一个List<String>的对象作为实际参数传进去，却发现无法通过编译。虽然从直觉上来说，Object是String的父类，这种类型转换应该是合理的。但是实际上这会产生隐含的类型转换问题，因此编译器直接就禁止这样的行为。本文试图对Java泛型做一个概括性的说明。

## 类型擦除
正确理解泛型概念的首要前提是理解类型擦除（`type erasure`）。 Java中的泛型基本上都是在编译器这个层次来实现的。在生成的Java字节代码中是不包含泛型中的类型信息的。使用泛型的时候加上的类型参数，会被编译器在编译的时候去掉。这个过程就称为类型擦除。如在代码中定义的List<Object>和List<String>等类型，在编译之后都会变成List。JVM看到的只是List，而由泛型附加的类型信息对JVM来说是不可见的。Java编译器会在编译时尽可能的发现可能出错的地方，但是仍然无法避免在运行时刻出现类型转换异常的情况。类型擦除也是Java的泛型实现方式与C++模板机制实现方式之间的重要区别。

 
很多泛型的奇怪特性都与这个类型擦除的存在有关，包括：

- 泛型类并没有自己独有的Class类对象。比如并不存在List<String>.class或是List<Integer>.class，而只有List.class。
- 静态变量是被泛型类的所有实例所共享的。对于声明为MyClass<T>的类，访问其中的静态变量的方法仍然是 MyClass.myStaticVar。不管是通过new MyClass<String>还是new MyClass<Integer>创建的对象，都是共享一个静态变量。
- **泛型的类型参数不能用在Java异常处理的catch语句中。因为异常处理是由JVM在运行时刻来进行的。由于类型信息被擦除，JVM是无法区分两个异常类型MyException<String>和MyException<Integer>的**。对于JVM来说，它们都是 MyException类型的。也就无法执行与异常对应的catch语句。

类型擦除的基本过程也比较简单，首先是找到用来替换类型参数的具体类。这个具体类一般是Object。如果指定了类型参数的上界的话，则使用这个上界。把代码中的类型参数都替换成具体的类。同时去掉出现的类型声明，即去掉<>的内容。比如T get()方法声明就变成了Object get()；List<String>就变成了List。

这里可能会困惑，为什么泛型类型被擦除了，我们在使用的时候又能准确拿到真实的类型?
编译后虽然擦除了真实类型，但是在使用时（如果有必要）会将其转成真实的类型，通过javap反编译后的字节码看这个问题就比较容易理解了

```java
public static void main(String[] args) {
        List<Integer> list = Lists.newArrayList(1, 2, 3);
        for (int i = 0; i < list.size(); i++) {
            Integer a = list.get(i);
            System.out.println(a);
        }
    }
// class version 52.0 (52)
// access flags 0x21
public class com/dempe/demo/TypeTest {

  // compiled from: TypeTest.java

  // access flags 0x1
  public <init>()V
   L0
    LINENUMBER 10 L0
    ALOAD 0
    INVOKESPECIAL java/lang/Object.<init> ()V
    RETURN
   L1
    LOCALVARIABLE this Lcom/dempe/demo/TypeTest; L0 L1 0
    MAXSTACK = 1
    MAXLOCALS = 1

  // access flags 0x9
  public static main([Ljava/lang/String;)V
   L0
    LINENUMBER 12 L0
    ICONST_3
    ANEWARRAY java/lang/Integer
    DUP
    ICONST_0
    ICONST_1
    INVOKESTATIC java/lang/Integer.valueOf (I)Ljava/lang/Integer;
    AASTORE
    DUP
    ICONST_1
    ICONST_2
    INVOKESTATIC java/lang/Integer.valueOf (I)Ljava/lang/Integer;
    AASTORE
    DUP
    ICONST_2
    ICONST_3
    INVOKESTATIC java/lang/Integer.valueOf (I)Ljava/lang/Integer;
    AASTORE
    INVOKESTATIC org/assertj/core/util/Lists.newArrayList ([Ljava/lang/Object;)Ljava/util/ArrayList;
    ASTORE 1
   L1
    LINENUMBER 13 L1
    ICONST_0
    ISTORE 2
   L2
   FRAME APPEND [java/util/List I]
    ILOAD 2
    ALOAD 1
    INVOKEINTERFACE java/util/List.size ()I
    IF_ICMPGE L3
   L4
    LINENUMBER 14 L4
    ALOAD 1
    ILOAD 2
    INVOKEINTERFACE java/util/List.get (I)Ljava/lang/Object;
    CHECKCAST java/lang/Integer
    ASTORE 3
   L5
    LINENUMBER 15 L5
    GETSTATIC java/lang/System.out : Ljava/io/PrintStream;
    ALOAD 3
    INVOKEVIRTUAL java/io/PrintStream.println (Ljava/lang/Object;)V
   L6
    LINENUMBER 13 L6
    IINC 2 1
    GOTO L2
   L3
    LINENUMBER 17 L3
   FRAME CHOP 1
    RETURN
   L7
    LOCALVARIABLE a Ljava/lang/Integer; L5 L6 3
    LOCALVARIABLE i I L2 L3 2
    LOCALVARIABLE args [Ljava/lang/String; L0 L7 0
    LOCALVARIABLE list Ljava/util/List; L1 L7 1
    // signature Ljava/util/List<Ljava/lang/Integer;>;
    // declaration: java.util.List<java.lang.Integer>
    MAXSTACK = 4
    MAXLOCALS = 4
}
```

##桥方法
泛型的类型擦除带来了不少问题。其中一个便是方法重写。
```java
public class Merchant<T extends Customer> {
    public double actionPrice(T customer) {
        return 0.0d;
    }
}

class VIPOnlyMerchant extends Merchant<VIP> {
    @Override
    public double actionPrice(VIP customer) {
        return 0.0d;
    }
}

class Customer{

}
class VIP extends Customer{}
```
VIPOnlyMerchant 中的 actionPrice 方法是符合 Java 语言的方法重写的，毕竟都使用 @Override 来注解了。然而，经过类型擦除后，父类的方法描述符为 (LCustomer;)D，而子类的方法描述符为 (LVIP;)D。这显然不符合 Java 虚拟机关于方法重写的定义。

为了保证编译而成的 Java 字节码能够保留重写的语义，Java 编译器额外添加了一个桥接方法。该桥接方法在字节码层面重写了父类的方法，并将调用子类的方法。

```
// class version 52.0 (52)
// access flags 0x20
// signature Lcom/dempe/demo/Merchant<Lcom/dempe/demo/VIP;>;
// declaration: com/dempe/demo/VIPOnlyMerchant extends com.dempe.demo.Merchant<com.dempe.demo.VIP>
class com/dempe/demo/VIPOnlyMerchant extends com/dempe/demo/Merchant  {

  // compiled from: Merchant.java

  // access flags 0x0
  <init>()V
   L0
    LINENUMBER 12 L0
    ALOAD 0
    INVOKESPECIAL com/dempe/demo/Merchant.<init> ()V
    RETURN
   L1
    LOCALVARIABLE this Lcom/dempe/demo/VIPOnlyMerchant; L0 L1 0
    MAXSTACK = 1
    MAXLOCALS = 1

  // access flags 0x1
  public actionPrice(Lcom/dempe/demo/VIP;)D
   L0
    LINENUMBER 15 L0
    DCONST_0
    DRETURN
   L1
    LOCALVARIABLE this Lcom/dempe/demo/VIPOnlyMerchant; L0 L1 0
    LOCALVARIABLE customer Lcom/dempe/demo/VIP; L0 L1 1
    MAXSTACK = 2
    MAXLOCALS = 2

  // access flags 0x1041
  public synthetic bridge actionPrice(Lcom/dempe/demo/Customer;)D
   L0
    LINENUMBER 12 L0
    ALOAD 0
    ALOAD 1
    CHECKCAST com/dempe/demo/VIP
    INVOKEVIRTUAL com/dempe/demo/VIPOnlyMerchant.actionPrice (Lcom/dempe/demo/VIP;)D
    DRETURN
   L1
    LOCALVARIABLE this Lcom/dempe/demo/VIPOnlyMerchant; L0 L1 0
    MAXSTACK = 2
    MAXLOCALS = 2
}

```
在我们的例子中，VIPOnlyMerchant 类将包含一个桥接方法 actionPrice(Customer)，它重写了父类的同名同方法描述符的方法。该桥接方法将传入的 Customer 参数强制转换为 VIP 类型，再调用原本的 actionPrice(VIP) 方法。

当一个声明类型为 Merchant，实际类型为 VIPOnlyMerchant 的对象，调用 actionPrice 方法时，字节码里的符号引用指向的是 Merchant.actionPrice(Customer) 方法。Java 虚拟机将动态绑定至 VIPOnlyMerchant 类的桥接方法之中，并且调用其 actionPrice(VIP) 方法。

需要注意的是，在 javap 的输出中，该桥接方法的访问标识符除了代表桥接方法的 ACC_BRIDGE 之外，还有 ACC_SYNTHETIC。它表示该方法对于 Java 源代码来说是不可见的。当你尝试通过传入一个声明类型为 Customer 的对象作为参数，调用 VIPOnlyMerchant 类的 actionPrice 方法时，Java 编译器会报错，并且提示参数类型不匹配。

```java
Customer customer = new VIP();
new VIPOnlyMerchant().actionPrice(customer); // 编译出错    
```

## 参考文档
http://www.infoq.com/cn/articles/cf-java-generics  
https://www.ibm.com/developerworks/cn/java/java-language-type-erasure/index.html


